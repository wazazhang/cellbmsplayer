<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml"  xmlns:ns1="Component.*" width="800" height="600" backgroundImage="image/bk.png"
		   verticalCenter="0" horizontalCenter="0">
	
	
	<mx:Script>
		<![CDATA[
			import Class.Game;
			import Class.Model.Desk;
			import Class.Model.Player;
			import Class.Server;
			import Class.TimesCtr;
			
			import com.fc.lami.Messages.PlayerData;
			
			import mx.controls.Alert;
			
			public var desk:Desk;
			
			public var player:PlayerData; //玩家
			[Bindable]
			public var seatID:int;
			
			public var nextOtherPlayer_Cpt:OtherPlayer_Cpt;
			[Bindable]
			
			public var game:Game
			
			
			//初始游戏
			
			public function init():void
			{
				game.matrix = mx;
				game.gamer.matrix = umx;
				game.timeCtr.optionTimeBar = optbar;
				game.timeCtr.sumTimeBar = sumbar;
				optCpt.game = game;
				
				game.initGame();
			}
			
			public function initDesk(desk:Desk):void
			{
				if(game.matrix==null)
				{
					init();
				}
				resetGameAllData();
				player = Server.player;
				initSeat(desk);
			}
			
			
			//进入一个新房间后对所有数据进行重置
			public function resetGameAllData():void
			{
				nextPlayer1.reset();
				nextPlayer2.reset();
				nextPlayer3.reset();
				game.cleanMatrix();
				game.gamer.cleanMatrix();
				game.gamer.isMyturn = false;
				game.gamer.isReady = false;
				game.gamer.isCold = true;
				game.legaled = true;
				game.haveSendCard = false;
				gameOverCvs.visible = false;
				game.timeCtr.stop();
			}
			
			//初始位置
			public function initSeat(desk:Desk):void
			{
				nextOtherPlayer_Cpt = nextPlayer1;
				nextPlayer1.nextOtherPlayer_Cpt = nextPlayer2;
				nextPlayer2.nextOtherPlayer_Cpt = nextPlayer3;
				this.desk = desk;
				switch(player.player_id)
				{
					case desk.player_E_id: seatID = 3;break;
					case desk.player_N_id: seatID = 0;break;
					case desk.player_S_id: seatID = 2;break;
					case desk.player_W_id: seatID = 1;break;
				}
				var cpt:OtherPlayer_Cpt = nextOtherPlayer_Cpt;
				var index:int = seatID;
				do{
					index ++;
					if(index==4)
						index = 0
					cpt.player = Server.getPlayer(getPlayerIDbySeatID(index));
					cpt.seat = getDir(index);
					cpt = cpt.nextOtherPlayer_Cpt;
				}
				while(cpt != null)
			}
			
			//根据座位号获取选手
			private function getPlayerIDbySeatID(seatID:int):int
			{
				switch(seatID)
				{
					case 3: return desk.player_E_id;
					case 0: return desk.player_N_id;
					case 2: return desk.player_S_id;
					case 1: return desk.player_W_id;
				}
				return -1;
			}
			
			//根据座位号设置选手
			private function setPlayerIDbySeatID(seatID:int,playerID:int):void
			{
				switch(seatID)
				{
					case 3: desk.player_E_id = playerID;break;
					case 0: desk.player_N_id = playerID;break;
					case 2: desk.player_S_id = playerID;break;
					case 1: desk.player_W_id = playerID;break;
				}
			}
			
			//根据座位号获取方向
			public function getDir(seatID:int):String
			{
				switch(seatID)
				{
					case 0: return "南";
					case 1: return "西";
					case 2: return "北";
					case 3: return "东";
				}
				return "false";
			}
		    			
			//有人员进入
			public function enterPlayer(player_id:int, desk_id:int, seat:int):void
			{
				if(desk ==null || desk_id!=desk.desk_id)
					return

				initSeat(desk);
				addInfo(Server.getPlayer(player_id).name+"进入桌子");
			}
			
			
			//有人员离开
			public function leavePlayer(player_id:int, desk_id:int):void
			{
				if(desk ==null || desk_id!=desk.desk_id)
					return	
				initSeat(desk);
				addInfo(Server.getPlayer(player_id).name+"离开桌子");
			}
			
			
			public function click():void
			{
				//Alert.show(Game.getSendPoint().toString()+'_'+Game.gamer.keydwon);
			}
			
			public function addInfo(str:String):void
			{
				gameinfo.text = str + "\n" +gameinfo.text ;
			}
			
			//回到房间
			public function returnRoom():void
			{
				//this.visible = false;
				Server.app.removeChild(this);
				Server.leaveDesk(desk.desk_id);
				
			}
			
			//根据用户ID获取用户组建
			public function getOtherPlayerCptByPlayerId(playerid:int):OtherPlayer_Cpt
			{
				var cpt:OtherPlayer_Cpt = nextPlayer1;
				do{
					if(cpt.player!=null && cpt.player.player_id==playerid)
					{
						return cpt;
					}
					cpt = cpt.nextOtherPlayer_Cpt;
				}
				while(cpt!=null)
				return null	
			}
			
			//有人员破冰
			public function onPlayerPoBing(playerid:int):void
			{
				if (playerid == Server.player.player_id){
					game.gamer.isCold = false;
				}else{
					var cpt:OtherPlayer_Cpt = getOtherPlayerCptByPlayerId(playerid);
					if(cpt!=null)
						cpt.pobing = true;
				}
				addInfo("玩家"+playerid+"已经破冰");
			}
			
			//有人员准备取消
			public function onPlayerReady(playerid:int,isReady:Boolean):void
			{
				if (playerid == Server.player.player_id){
					game.gamer.isReady = isReady;
				}else{
					var cpt:OtherPlayer_Cpt = getOtherPlayerCptByPlayerId(playerid);
					if(cpt!=null)
						cpt.ready = isReady;
				}
				if(isReady)
				{
					addInfo("玩家"+playerid+"准备好了");
				}
				else
				{
					addInfo("玩家"+playerid+"取消了准备");
				}
			}
			
			//有人员会合开始
			public function onPlayerStart(playerid:int):void
			{
				nextPlayer1.gameing = false;
				nextPlayer2.gameing = false;
				nextPlayer3.gameing = false;
				
				var cpt:OtherPlayer_Cpt = getOtherPlayerCptByPlayerId(playerid);
				cpt.gameing = true;
			}
			
			//游戏结束
			public function onGameOver():void
			{
				gameOverCvs.visible = true;
			}
		]]>
	</mx:Script>
	
	<ns1:Matrix_Cpt id="mx" y="100" borderStyle="solid" height="201" x="30">
	</ns1:Matrix_Cpt>
	
	<ns1:UserMatrix_Cpt id="umx" height="202" x="342" y="389" width="400">
		<mx:ProgressBar id="sumbar" width="364" label="" height="10" mode="manual" labelPlacement="center" y="24" x="0"/>
		<mx:ProgressBar id="optbar" height="10" width="364" label="" mode="manual" labelPlacement="center" x="0" y="36"/>
	</ns1:UserMatrix_Cpt>
	
	<ns1:Opt_Cpt id="optCpt" height="177" y="411" x="704">
	</ns1:Opt_Cpt>
	
	<mx:Label x="268" y="536" text="{game.gamer.handCard.length}" color="#FDFDFD"/>
	<mx:TextArea id="gameinfo" x="29" y="429" width="195" height="113" backgroundAlpha="0.0" color="#FDFFFF" editable="false"/>
	
	<ns1:OtherPlayer_Cpt id="nextPlayer1" x="579" y="100">
	</ns1:OtherPlayer_Cpt>
	
	<ns1:OtherPlayer_Cpt id="nextPlayer2"  x="579" y="203">
	</ns1:OtherPlayer_Cpt>
	
	<ns1:OtherPlayer_Cpt id="nextPlayer3" x="579" y="305">
	</ns1:OtherPlayer_Cpt>
	
	<mx:Label x="268" y="520" text="{getDir(seatID)+':'+ Server.player.name}" color="#FCFDFD"/>
	
	<mx:Label x="268" y="550" text="{game.gamer.isMyturn?'游戏中':'等待中'}" color="#FCFDFD"/>
	<mx:LinkButton x="10" y="10" label="回到大厅" color="#F9FBFC" click="returnRoom()"/>
	
	<mx:Canvas id="gameOverCvs" visible="false" width="200" height="100" verticalCenter="0" horizontalCenter="0" backgroundColor="#FE0303" backgroundAlpha="0.28" borderStyle="solid" cornerRadius="5">
		<mx:Label text="游戏结束" color="#FDFDFD" fontSize="16" horizontalCenter="0" verticalCenter="0"/>
	</mx:Canvas>
	
	
</mx:Canvas>
